* Chris Lloyd's Emacs Configuration
** Package Management
Setup package management.

#+begin_src emacs-lisp
  (defvar bootstrap-version)
  (let ((bootstrap-file
	  (expand-file-name
	  "straight/repos/straight.el/bootstrap.el"
	  (or (bound-and-true-p straight-base-dir)
	      user-emacs-directory)))
      (bootstrap-version 7))
  (unless (file-exists-p bootstrap-file)
      (with-current-buffer
	  (url-retrieve-synchronously
	  "https://raw.githubusercontent.com/radian-software/straight.el/develop/install.el"
	  'silent 'inhibit-cookies)
      (goto-char (point-max))
      (eval-print-last-sexp)))
  (load bootstrap-file nil 'nomessage))

  (package-initialize)

  (require 'package)
  (add-to-list 'package-archives '("melpa" . "https://melpa.org/packages/"))
  (add-to-list 'package-archives '("gnu" . "http://elpa.gnu.org/packages/"))

   (unless (package-installed-p 'use-package)
       (package-refresh-contents)
       (package-install 'use-package))

  (require 'use-package)

  (setq use-package-always-ensure t)
#+end_src

#+RESULTS:
: t

** General Settings
*** Customs File
When changing a variable through customize-variable dialog save these
changes in the custom.el instead of appending them to the end of the
init file.
#+begin_src emacs-lisp
  (setq custom-file "~/.emacs.d/custom.el")
  (load custom-file)
#+end_src

#+RESULTS:
: t

*** Backups
Set the emacs backup to stop cluttering folders with backup files.
#+begin_src emacs-lisp
  (setq backup-directory-alist `(("." . "~/.emacs_backups")))
  (setq backup-by-copying t)
  (setq delete-old-versions t
    kept-new-versions 6
    kept-old-versions 2
    version-control t)
  (setq vc-make-backup-files t)
#+end_src

#+RESULTS:
: t

*** No Bell 
Turn off both the bell sound and the visual indicator. 
#+begin_src emacs-lisp
  (setq visible-bell       nil
	ring-bell-function #'ignore)
#+end_src

#+RESULTS:
: ignore

** UI
*** Recentf
Builds a list of the recently opened files.
#+begin_src emacs-lisp

  (use-package recentf
    :config
    (setq recentf-max-saved-items nil))
#+end_src

#+RESULTS:
: t

*** Ivy
#+begin_src emacs-lisp

  (use-package ivy
    :ensure t
    :config
    (ivy-mode 1)
    (setq ivy-use-virtual-buffers t)
    (setq ivy-count-format "(%d/%d) "))

  (use-package avy
    :ensure t)

  (use-package ace-window
    :ensure t
    :init
    (setq aw-dispatch-always t))

  (use-package counsel
    :ensure t
    :config
    (counsel-mode 1))

  (use-package ace-link)

  (use-package which-key
    :ensure t
    :config
    (which-key-mode 1))

  (use-package swiper
    :ensure t
    :config)

  (use-package ace-popup-menu
    :ensure t
    :config
    (ace-popup-menu-mode 1))

  (use-package ivy-posframe
    :config
    (ivy-posframe-mode)
    (set-face-attribute 'ivy-posframe nil :background "DarkSlateBlue")
    (setq ivy-posframe-display-functions-alist '((t . ivy-posframe-display))
	  ivy-posframe-height-alist '((t . 5))
	  ivy-posframe-parameters '((internal-border-width . 5))))

  (setq posframe-arghandler #'my-posframe-arghandler)
  (defun my-posframe-arghandler (buffer-or-name arg-name value)
    (let ((info '(:internal-border-width 0 :background-color "DarkSlateBlue")))
      (or (plist-get info arg-name) value)))
#+end_src

#+RESULTS:
: my-posframe-arghandler

*** 

*** Window Management Setup
When working with really long lines it is useful to be able to center
yourself horizontally on the line. For me this mainly occurs in large
tikz-diagrams, where I disabled wrapping. This function is from [[https://stackoverflow.com/questions/1249497/command-to-center-screen-horizontally-around-cursor-on-emacs][here]].
#+BEGIN_SRC emacs-lisp
  (winner-mode)

  (defun my-horizontal-recenter ()
    "Make the point horizontally centered in the window."
    (interactive)
    (let ((mid (/ (window-width) 2))
	(line-len (save-excursion (end-of-line) (current-column)))
	(cur (current-column)))
      (if (< mid cur)
	(set-window-hscroll (selected-window)
			    (- cur mid)))))
#+END_SRC

#+RESULTS:
: my-horizontal-recenter

#+begin_src emacs-lisp
  ;; (split-window-horizontally)
  ;; (split-window-horizontally)
  ;; (balance-windows)
#+end_src

#+RESULTS:

*** Evil
#+begin_src emacs-lisp
    (use-package undo-fu)

    (use-package evil
	:config
	(setq evil-undo-system 'undo-fu)
	(setq evil-want-visual-char-semi-exclusive t)
	(setq evil-want-fine-undo t)
	(evil-mode 1))

    (use-package evil-smartparens)

    ;; (use-package evil-magit)

    (use-package evil-org
	:ensure t
	:after org
	:config
	(add-hook 'org-mode-hook 'evil-org-mode)
	;; (add-hook 'evil-org-mode-hook
	;; 	    (lambda ()
	;; 	    (evil-org-set-key-theme)))
	(require 'evil-org-agenda)
	(evil-org-agenda-set-keys))

    (use-package evil-commentary
	:config
	(evil-commentary-mode))

  (use-package evil-collection)
  (evil-collection-init 'magit)

    (use-package evil-args
	:init
	(define-key evil-inner-text-objects-map "a" 'evil-inner-arg)
	(define-key evil-outer-text-objects-map "a" 'evil-outer-arg))

    (use-package key-chord
	:config
	(key-chord-mode 1)
	(key-chord-define evil-insert-state-map  "jk" 'evil-normal-state)
	(key-chord-define evil-replace-state-map  "jk" 'evil-normal-state)
	(key-chord-define ivy-minibuffer-map  "jk" 'keyboard-escape-quit)
	;; (add-hook 'evil-lispy-mode-hook '(key-chord-define evil-lispy-mode-map  "jk" 'evil-normal-state))
	(key-chord-define evil-visual-state-map  "jk" 'evil-normal-state))

    (use-package evil-matchit)
#+end_src

#+RESULTS:

*** Surrounds
#+BEGIN_SRC emacs-lisp
  (use-package smartparens-config
	     :ensure smartparens
	     :config
	     (setq sp-max-pair-length 20)
	     (sp-pair "\\left\\langle" "\\right\\rangle")
	     (sp-pair "\\left(" "\\right)")
	     (sp-pair "\\left\\{" "\\right\\}")
	     ;; (sp-pair "|" "|")
	     (sp-pair "\\left|" "\\right|")
	     (sp-pair "\\left\\langle" "\\right\\rangle")
	     (sp-pair "\\left\\lceil" "\\right\\rceil")
	     (sp-pair "\\left\\lfloor" "\\right\\rfloor")
	     (smartparens-global-mode 1))

  (use-package evil-surround
    :ensure t
    :config
    (global-evil-surround-mode 1)
      (setq-default evil-surround-pairs-alist
	  '((?\( . ("(" . ")"))
	  (?\[ . ("[ " . " ]"))
	  (?\{ . ("{" . "}"))
	  (?\) . ("( " . " )"))
	  (?\] . ("[" . "]"))
	  (?\} . ("{ " . " }"))
	  (?# . ("#{" . "}"))
	  (?b . ("(" . ")"))
	  (?B . ("{" . "}"))
	  (?> . ("<" . ">"))
	  (?t . evil-surround-read-tag)
	  (?< . evil-surround-read-tag)
	  (?\C-f . evil-surround-prefix-function)
	  (?f . evil-surround-function))))

  (use-package embrace
   :ensure t
   :config
   (add-hook 'LaTeX-mode-hook (lambda ()
     (embrace-add-pair ?e "\\begin{" "}")
     (embrace-add-pair ?a "\\begin{align*}\n" "\n\\end{align*}")
     (embrace-add-pair ?m "\\(" "\\)")
     (embrace-add-pair ?M "\\[" "\\]")
     (embrace-add-pair ?l "\\left\(" "\\right\)")
     (defun embrace-with-command ()
     (let ((fname (read-string "Command: ")))
       (cons (format "\\%s{" (or fname "")) "}")))
     (embrace-add-pair-regexp ?c "\\\\\\w+?{" "}" 'embrace-with-command
			    (embrace-build-help "\\command{" "}")))))

   (use-package evil-embrace
    :init
    (evil-embrace-enable-evil-surround-integration)
    (setq evil-embrace-show-help-p nil))
#+END_SRC

#+RESULTS:

*** Global Keybindings

#+begin_src emacs-lisp
  (use-package general)

  (define-key evil-insert-state-map (kbd "<escape>") nil)

  (general-define-key
   :keymaps 'normal
   "z =" 'mk-flyspell-correct-previous
   "z \\" 'powerthesaurus-lookup-word-at-point
   "z Z" 'my-horizontal-recenter
   "g o" 'my-goto-defn-split
   "g a" 'evil-forward-arg
   "g A" 'evil-backward-arg
   "<deletechar>" '(lambda () (interactive) (switch-to-buffer-other-window "*Sage*")))
  
  (general-create-definer my-leader-def
    :states '(normal emacs visual)
    :keymaps '(global magit-mode-map)
    :prefix "SPC")

  (my-leader-def
    "m" 'magit-status
    "y"   'counsel-yank-pop
    "b b" 'switch-to-buffer
    "b k" 'kill-buffer
    "w" (general-key-dispatch 'ace-window
	  "w" 'ace-window
	  "f" 'fit-window-to-buffer
	  "<SPC>" 'delete-other-windows
	  "k" 'delete-window
	  "h" 'split-window-vertically
	  "v" 'split-window-horizontally
	  "s" 'ace-swap-window
	  "<left>" 'shrink-window-horizontally
	  "<right>" 'enlarge-window-horizontally
	  "<down>" 'shrink-window
	  "<up>" 'enlarge-window
	  "b" 'balance-windows
	  "u" 'winner-undo
	  "r" 'winner-redo)
    "<SPC>" 'avy-goto-char
    "l" 'avy-goto-line
    "f f" 'counsel-find-file
    "f r" 'counsel-recentf
    "f s" 'save-buffer
    "f S" 'write-file
    "x" 'counsel-M-x
    "h k" 'describe-key
    "h f" 'describe-function
    "h m" 'describe-mode
    "h v" 'describe-variable
    ":" 'eval-expression
    "s f" 'sp-slurp-hybrid-sexp
    "s b" 'sp-forward-barf-sexp
    "s F" 'sp-backward-slurp-sexp
    "s B" 'sp-backward-barf-sexp
    "/" 'swiper-all
    "d" 'counsel-dash-at-point
    "D" 'counsel-dash)

  (general-define-key
   :keymaps 'ivy-mode-map
   "C-j" 'ivy-next-line
   "C-k" 'ivy-previous-line)

  (general-define-key
   :states '(normal emacs)
   :keymaps '(sage-shell-mode-map gap-process-map)
   "SPC p" 'counsel-shell-history)
#+end_src

#+RESULTS:

*** 

** Major Modes
*** C++
**** Keybindings
#+begin_src emacs-lisp
  (my-leader-def 'c++-mode-map
    "c" 'compile)
#+end_src

#+RESULTS:

*** LaTeX
**** Setup
#+begin_src emacs-lisp
  (use-package tex
    :ensure auctex
    :init
    (add-hook 'LaTeX-mode-hook (lambda () (flycheck-select-checker 'tex-chktex)))
    ()
    (setq TeX-auto-save t)
    (setq TeX-parse-self t)
    (setq-default TeX-master nil)

    (add-hook 'LaTeX-mode-hook 'server-start)
    (add-hook 'LaTeX-mode-hook 'TeX-source-correlate-mode)

    (add-hook 'LaTeX-mode-hook 'turn-on-reftex)
    (setq reftex-plug-into-AUCTeX t)
    (setq flycheck-tex-chktex-executable "/Library/TeX/texbin/chktex")
    (add-hook 'LaTeX-mode-hook 'flyspell-mode)
    ;; https://tex.stackexchange.com/questions/69555/i-want-to-disable-auto-fill-mode-when-editing-equations
    (defvar my-LaTeX-no-autofill-environments
      '("equation" "equation*" "align*" "tikzcd")
      "A list of LaTeX environment names in which `auto-fill-mode' should be inhibited.")

    (defun my-LaTeX-auto-fill-function ()
      "This function checks whether point is currently inside one of
	the LaTeX environments listed in
	`my-LaTeX-no-autofill-environments'. If so, it inhibits automatic
	filling of the current paragraph."
      (let ((do-auto-fill t)
	    (current-environment "")
	    (level 0))
	(while (and do-auto-fill (not (string= current-environment "document")))
	  (setq level (1+ level)
		current-environment (LaTeX-current-environment level)
		do-auto-fill (not (member current-environment my-LaTeX-no-autofill-environments))))
	(when do-auto-fill
	  (do-auto-fill))))

    (defun my-LaTeX-setup-auto-fill ()
      "This function turns on auto-fill-mode and sets the function
	used to fill a paragraph to `my-LaTeX-auto-fill-function'."
      (auto-fill-mode)
      (setq auto-fill-function 'my-LaTeX-auto-fill-function))

    (add-hook 'LaTeX-mode-hook 'my-LaTeX-setup-auto-fill))

  (defun my-latex-evil-create-environment ()
    "Create environment and enter insert mode."
    (interactive)
    (progn
      (call-interactively 'LaTeX-environment)
      (call-interactively 'evil-insert)))

  (defun my-latex-evil-modify-current-environment ()
    "Change current environment."
    (interactive)
    (let ((current-prefix-arg 4))
      (call-interactively #'LaTeX-environment)))

  (defun my-latex-insert-inline-math ()
    "Insert \(\)."
    (interactive)
    (progn (insert "\\(\\)")
	   (evil-backward-char)
	   (call-interactively 'evil-insert)))

  (defun my-latex-insert-display-math ()
    "Insert \[\]."
    (interactive)
    (progn
      (if (not (string= (thing-at-point 'line t) "\n"))
	  (call-interactively 'evil-open-below))
      (insert "\\[\\]")
      (evil-backward-char)
      (call-interactively 'evil-insert)))
#+end_src

#+RESULTS:
: my-latex-insert-display-math

This function searches the current LaTeX project for all the labels
and then lets you insert one using the completion framework of your
choice (in my case it uses ivy). This is based on the reftex function
goto-label.

#+begin_src emacs-lisp 
(defun my-ref-label (&optional other-window)
  "Prompt for a label (with completion) and insert a reference to it."
  (interactive "P")
  (reftex-access-scan-info)
  (let* ((docstruct (symbol-value reftex-docstruct-symbol))
	 ;; If point is inside a \ref{} or \pageref{}, use that as
	 ;; default value.
	 (default (when (looking-back "\\\\\\(?:page\\)?ref{[-a-zA-Z0-9_*.:]*"
                                      (line-beginning-position))
		    (reftex-this-word "-a-zA-Z0-9_*.:")))
         (label (completing-read (if default
				     (format "Label (default %s): " default)
				   "Label: ")
				 docstruct
                                 (lambda (x) (stringp (car x))) t nil nil
				 default)))
    (insert (concat "\\ref{" label "}"))))
#+end_src

#+RESULTS:
: my-ref-label

**** Keybindings
#+begin_src emacs-lisp
  (my-leader-def 'LaTeX-mode-map
    "c" 'TeX-command-master
    "v v" 'preview-buffer
    "v c" 'preview-clearout
    "t" 'TeX-next-error
    "e e" 'my-latex-evil-create-environment
    "e m" 'my-latex-evil-modify-current-environment
    "e c" 'LaTeX-close-environment
    "r r" 'ivy-bibtex-with-local-bibliography
    "r l" 'my-ref-label)
#+end_src

#+RESULTS:

**** Bibtex
#+begin_src emacs-lisp
  (use-package ivy-bibtex
     :config
     (setq ivy-re-builders-alist
	 '((ivy-bibtex . ivy--regex-ignore-order)
	 (t . ivy--regex-plus))))

  (setq ivy-bibtex-default-action 'ivy-bibtex-insert-citation)

  (use-package gscholar-bibtex)
#+end_src

#+RESULTS:

**** PDF View
#+begin_src emacs-lisp
  (use-package pdf-tools
    :config
    (pdf-tools-install)
    (setq TeX-view-program-selection '((output-pdf "PDF Tools"))
	TeX-source-correlate-start-server t)
    (add-hook 'TeX-after-compilation-finished-functions
	#'TeX-revert-document-buffer))
#+end_src

#+RESULTS:
: t

I wrote a small package called [[https://github.com/cjl8zf/evil-pdf-tools][evil-pdf-tools]] that adds vim style key
bindings for navigating pdfs and also simulates the vim search for
pdfs.
#+begin_src emacs-lisp
  ;; (require 'evil-pdf-tools "~/.emacs.d/evil-pdf-tools-0.0.1.el")
#+end_src

#+RESULTS:

*** Sage
**** Setup
#+begin_src emacs-lisp
  (use-package sage-shell-mode
      :init
      (setq sage-shell:sage-root "/Applications/SageMath2/"))

  (defun send-to-sage-and-switch ()
      "Send buffer to sage and switch to sage buffer."
      (interactive)
      (progn
      (caill-interactively 'sage-shell-edit:send-buffer)
      (call-interactively 'other-window)))
#+end_src

#+RESULTS:
: send-to-sage-and-switch

**** Generate Ctags
#+BEGIN_SRC emacs-lisp
  (defun generate-sage-tags ()
    "Generate a tags file for all *.sage files in current directory."
    (interactive)
    (shell-command "find . -name \"*.sage\" -print | etags -l \"python\" -"))
#+END_SRC

#+RESULTS:
: generate-sage-tags

**** Keybindings
#+begin_src emacs-lisp
  (my-leader-def 'sage-shell:sage-mode-map
    "c" 'sage-shell-edit:send-buffer)
#+end_src

#+RESULTS:

*** Sage

**** Keybindings
#+begin_src emacs-lisp
  (my-leader-def 'python-mode-map
    "c" 'python-shell-send-buffer)
#+end_src

#+RESULTS:

*** Gap
**** Setup
#+begin_src emacs-lisp
  (use-package gap-mode
    :init
    (setq gap-executable "/Applications/gap/bin/gap.sh"))
#+end_src

#+RESULTS:

**** Keybindings
#+begin_src emacs-lisp
  (my-leader-def 'gap-mode-map
    "c" 'gap-eval-file)
#+end_src

#+RESULTS:

*** Haskell
**** Setup
#+begin_src emacs-lisp
  (use-package haskell-mode
    :ensure t
    :init
    (add-hook 'haskell-mode-hook 'interactive-haskell-mode)
    (add-hook 'haskell-mode-hook 'haskell-indent-mode)
    (with-eval-after-load "haskell-mode"
      ;; This changes the evil "O" and "o" keys for haskell-mode to make sure that
      ;; indentation is done correctly. See
      ;; https://github.com/haskell/haskell-mode/issues/1265#issuecomment-252492026.
      (defun haskell-evil-open-above ()
	(interactive)
	(evil-digit-argument-or-evil-beginning-of-line)
	(haskell-indentation-newline-and-indent)
	(evil-previous-line)
	(haskell-indentation-indent-line)
	(evil-append-line nil))

      (defun haskell-evil-open-below ()
	(interactive)
	(evil-append-line nil)
	(haskell-indentation-newline-and-indent))))
#+end_src

#+RESULTS:

**** Keybindings
#+begin_src emacs-lisp
  (evil-define-key 'normal haskell-mode-map
     "o" 'haskell-evil-open-below
     "O" 'haskell-evil-open-above)
     
  (my-leader-def 'haskell-mode-map 
      "c" 'haskell-process-load-file)
  (my-leader-def 'haskell-error-mode-map
  "q" 'quit-window) 
#+end_src

#+RESULTS:

*** Org

#+begin_src emacs-lisp
  (use-package org
    :ensure t
    :init
    (define-key global-map "\C-cl" 'org-store-link)
    (define-key global-map "\C-ca" 'org-agenda)
    (bind-key* "C-c c" 'org-capture)
    (setq org-hide-emphasis-markers 't)
    (setq org-capture-templates
	  '(("t" "Todo" entry (file "~/org/refile.org")
	     "* TODO %?\n")
	    ("c" "Clocked Todo" entry (file "~/org/refile.org")
	     "* TODO %?\n" :clock-in t :clock-keep t)))
    (setq org-agenda-files '("~/org"))
    ;; 12 hour clock
    (setq org-agenda-timegrid-use-ampm 1)
    (setq org-refile-targets '((nil :maxlevel . 9)
			       (org-agenda-files :maxlevel . 9)))
    ;; hide file names in agenda view
    (setq org-agenda-prefix-format "%t %s")
    (setq org-reverse-note-order t)
    (setq org-clock-persist 'history)
    (org-clock-persistence-insinuate)
    (add-hook 'org-mode-hook #'smartparens-mode)
    (eval-after-load 'org
      '(setf org-highlight-latex-and-related '(latex)))
    (add-hook 'org-mode-hook 'flyspell-mode)
    (add-hook 'org-mode-hook 'turn-on-auto-fill)
    ;; (setq org-modules (cons 'org-habit org-modules))
    (setq org-latex-create-formula-image-program 'imagemagick)
    (setq org-tags-column -90)
    (setq org-format-latex-options (plist-put org-format-latex-options :scale 2.0))
    :config
    (with-eval-after-load 'ox-latex
      (add-to-list 'org-latex-classes '("draft" "\\documentclass[11pt,draft]{book}"
					("\\chapter{%s}" . "\\chapter*{%s}")
					("\\section{%s}" . "\\section*{%s}")
					("\\subsection{%s}" . "\\subsection*{%s}")
					("\\subsubsection{%s}" . "\\subsubsection*{%s}")
					("\\paragraph{%s}" . "\\paragraph*{%s}")
					("\\subparagraph{%s}" . "\\subparagraph*{%s}")))
      (add-to-list 'org-latex-classes '("semesternotes" "\\documentclass[11pt]{book}"
					("\\part{%s}" . "\\part*{%s}")
					("\\chapter{%s}" . "\\chapter*{%s}")
					("\\section{%s}" . "\\section*{%s}")
					("%% %s" . "%% %s")
					("%% %s" . "%% %s")
					("\\paragraph{%s}" . "\\paragraph*{%s}")
					("\\subparagraph{%s}" . "\\subparagraph*{%s}")))
      (add-to-list 'org-latex-classes '("drill" "\\documentclass[11pt]{book}"
					("\\section{%s}" . "\\section*{%s}")
					("%% %s" . "%% %s")
					("\\begin{thm}{%s}" "\\end{thm}" "\\begin{thm}{%s}" "\\end{thm}")
					("\\begin{proof}{%s}" "\\end{proof}" "\\begin{proof}{%s}" "\\end{proof}"))))

    ;; ("\\begin{defn}{%s}" "\\end{defn}" "\\begin{defn}{%s}" "\\end{defn}")
    (setq org-catch-invisible-edits 'error)
    (setq org-ctrl-k-protect-subtree t)
    :bind* (("C-c C-x o" . org-clock-out)))

  ;; (use-package org-mru-clock
  ;;   :config
  ;;     (setq org-mru-clock-completing-read #'ivy-completing-read))
  ;; (use-package org-drill
  ;;   :ensure org-plus-contrib
  ;;   :config
  ;;   (setq org-drill-add-random-noise-to-intervals-p t))

  ;; (use-pa ckage org-edit-latex)

  (defun my-org-hide-current-body ()
    "Hide body of current heading and go to heading."
    (interactive)
    (progn
      (call-interactively 'org-previous-visible-heading)
      (call-interactively 'org-cycle)))

  (defun my-evil-org-insert-heading-after ()
    "Insert org heading after current heading and go into insert mode."
    (interactive)
    (progn
      (call-interactively 'org-insert-heading-after-current)
      (call-interactively 'evil-insert)))

  (defun my-evil-org-insert-heading-before ()
    "Insert org heading above current heading and go into insert mode."
    (interactive)
    (progn
      (call-interactively 'beginning-of-line)
      (call-interactively 'org-insert-heading)
      (call-interactively 'evil-insert)))

  (defun my-evil-org-insert-subheading ()
    "Create org subheading and enter insert mode."
    (interactive)
    (progn
      (call-interactively 'end-of-line)
      (call-interactively 'org-insert-subheading)
      (call-interactively 'evil-insert)))
#+end_src

#+RESULTS:
: my-evil-org-insert-subheading

**** Keybindings
#+begin_src emacs-lisp
  (my-leader-def 'org-mode-map
    "<RET>" 'my-evil-org-insert-subheading
    "T" 'org-todo
    "t" 'counsel-org-tag
    ;; "rf" 'org-refile
    "E" 'org-export-dispatch
    "e" 'org-edit-special
    "o" 'my-evil-org-insert-heading-after
    "O" 'my-evil-org-insert-heading-before
    "." 'my-evil-org-insert-subheading
    "p" 'org-toggle-latex-fragment
    "rr" 'org-roam
    "rf" 'org-roam-find-file
    "rb" 'org-roam-switch-to-buffer
    "rg" 'org-roam-graph-show
    "ri" 'org-roam-insert
    "cb" 'org-babel-execute-src-block)

    (setq org-confirm-babel-evaluate nil)
  
  (general-define-key
   :states 'normal
   :keymaps 'org-mode-map
   "g k"  'org-backward-heading-same-level
   "g j"  'org-forward-heading-same-level
   "g h" 'outline-up-heading
   "g l" 'outline-next-visible-heading
   "g J" 'my-org-hide-current-body)
#+end_src

#+RESULTS:

*** Elisp
**** Setup
#+BEGIN_SRC emacs-lisp
  (use-package lispy)
  (use-package evil-lispy)
  (use-package lispyville
    :init
    (add-hook 'lispy-mode-hook #'lispyville-mode))

  (use-package paredit)
#+END_SRC

#+RESULTS:

**** Keybindings
#+BEGIN_SRC emacs-lisp
  (my-leader-def 'emacs-lisp-mode-map
    "e b" 'eval-buffer)
#+END_SRC

#+RESULTS:

*** Lean 4
#+BEGIN_SRC emacs-lisp
      (setq straight-built-in-pseudo-packages '(emacs xref nadvice python image-mode project flymake))
      (use-package lean4-mode
	:commands lean4-mode
	:straight (lean4-mode :type git :host github
			    :repo "leanprover-community/lean4-mode"
			    :files ("*.el" "data")))

  ;; (use-package nael
  ;;   :straight (:host codeberg 
  ;;              :repo "mekeor/nael"
  ;;              :files (:defaults (:exclude "build.el"))))
   ;; (add-to-list 'load-path "~/.emacs.d/nael/nael")
   ;; (add-to-list 'load-path "~/.emacs.d/nael/nael-lsp")
   ;; (add-to-list 'load-path "~/.emacs.d/nael/nael-markdown")
   ;; (require 'nael)
   ;; (require 'nael-lsp)
   ;; (require 'nael-markdown)
   ;; (add-hook 'nael-mode-hook #'abbrev-mode)
   ;; (add-hook 'nael-mode-hook #'eglot-ensure)

    (add-hook 'lean4-mode-hook #'lsp)   ;; for Lean 4

    (defun lean-toggle-check ()
      (interactive)
      (save-excursion
	(beginning-of-line)
	(cond
	 ;; #check → remove
	 ((looking-at "^#check\\(\\s-+\\)?")
	  (replace-match ""))
	 ;; #eval → replace with #check
	 ((looking-at "^#eval\\(\\s-+\\)?")
	  (replace-match "#check "))
	 ;; other → insert #check
	 (t
	  (insert "#check ")))))

    (defun lean-toggle-eval ()
      (interactive)
      (save-excursion
	(beginning-of-line)
	(cond
	 ;; #eval → remove
	 ((looking-at "^#eval\\(\\s-+\\)?")
	  (replace-match ""))
	 ;; #check → replace with #eval
	 ((looking-at "^#check\\(\\s-+\\)?")
	  (replace-match "#eval "))
	 ;; other → insert #eval
	 (t
	  (insert "#eval ")))))
#+END_SRC

#+RESULTS:
: lean-toggle-eval

**** Keybindings
#+begin_src emacs-lisp
  (my-leader-def 'lean4-mode-map
    "l c" 'lean-toggle-check
    "l e" 'lean-toggle-eval
    "l l" 'lean4-toggle-info
    "?" 'lsp-ui-doc-mode)
#+end_src

#+RESULTS:

**** `jk` normal mode fix 

Lean4-mode uses quail to replace LaTeX-like symbols \alpha with the
unicode equivalents. This interferes with key-chored mode which also
uses quail under the hood. This function adds jk directly to the quail
map in lean4-mode so that jk keeps working. 

#+begin_src emacs-lisp :lexical t 
     (defun quail-jk (key idx) 
       (let ((curpos (point)))
	 (quail-delete-region)
	 (evil-normal-state)
	 (run-at-time 0.01 nil (lambda () (goto-char (- curpos (if (bolp) 1 2)))))
	 (throw 'quail-tag nil))
       )

    (add-hook 'lean4-mode-hook (lambda ()
	(setq lsp-inlay-hint-enable nil)
	(setq lsp-warn-no-matched-method nil)
	(quail-define-rules ((append . t)) ("jk" quail-jk))))
#+end_src

#+RESULTS:
| (closure (t) nil (setq lsp-inlay-hint-enable nil) (setq lsp-warn-no-matched-method nil) (quail-define-rules ((append . t)) (jk quail-jk))) | (lambda nil (setq lsp-inlay-hint-enable nil) (setq lsp-warn-no-matched-method nil) (let ((tail '((jk quail-jk))) (map (quail-map)) (decode-map (and (quail-decode-map) (not nil))) (properties 'nil) key trans) (while tail (progn (setq key (car (car tail))) (setq trans (car (cdr (car tail)))) (setq tail (cdr tail))) (quail-defrule-internal key trans map t decode-map properties)))) | lsp |

** Documentation
We will use helm together with helm-dash to view documentation in
emacs.
#+begin_src emacs-lisp
  (setq helm-dash-common-docsets '("Python 2" "Haskell" "Sage" "Emacs Lisp"))
#+end_src

#+RESULTS:
| Python 2 | Haskell | Sage | Emacs Lisp |


We will display the documentation in the eww web-browser in emacs.

We set the font to match the rest of emacs instead of the font
provided from html. We also force the browser to pop up on the left if
possible otherwise split vertically.
#+begin_src emacs-lisp
  (setq counsel-dash-browser-func 'eww-browse-url)

  (add-hook 'eww-mode-hook 'eww-toggle-fonts)

  (defun eww-display+ (buf _alist)
    (let ((w (or
	       (window-in-direction 'left)
	       (split-window-vertically))))
      (set-window-buffer w buf) w))

  (push `(,(rx "*eww*")
	(eww-display+))
	display-buffer-alist)
#+end_src

#+RESULTS:
| \*eww\* | (eww-display+) |
| \*eww\* | (eww-display+) |

** Completions
*** Yasnippet
#+begin_src emacs-lisp
  (use-package yasnippet
    :ensure t
    :init
    (yas-global-mode 1)
    (defun my-org-latex-yas ()
      "Activate org and LaTeX yas expansion in org-mode buffers."
      (yas-minor-mode)
      (yas-activate-extra-mode 'latex-mode))

    (add-hook 'org-mode-hook #'my-org-latex-yas))

  (use-package yasnippet-snippets
    :ensure t)
#+end_src

#+RESULTS:

*** Hippie Expand
#+begin_src emacs-lisp
  (global-set-key (kbd "M-/") 'hippie-expand)

  ;; https://blog.binchen.org/posts/autocomplete-with-a-dictionary-with-hippie-expand.html
  ;; The actual expansion function
  (defun try-expand-by-dict (old)
    ;; old is true if we have already attempted an expansion
    (unless (bound-and-true-p ispell-minor-mode)
      (ispell-minor-mode 1))

    ;; english-words.txt is the fallback dicitonary
    (if (not ispell-alternate-dictionary)
	(setq ispell-alternate-dictionary (file-truename "~/.emacs.d/misc/english-words.txt")))
    (let ((lookup-func (if (fboundp 'ispell-lookup-words)
			   'ispell-lookup-words
			 'lookup-words)))
      (unless old
	(he-init-string (he-lisp-symbol-beg) (point))
	(if (not (he-string-member he-search-string he-tried-table))
	    (setq he-tried-table (cons he-search-string he-tried-table)))
	(setq he-expand-list
	      (and (not (equal he-search-string ""))
		   (funcall lookup-func (concat (buffer-substring-no-properties (he-lisp-symbol-beg) (point)) "*")))))

      (if (null he-expand-list)
	  (if old (he-reset-string))
	(he-substitute-string (car he-expand-list))
	(setq he-expand-list (cdr he-expand-list)) t)))

    (global-set-key (kbd "M-/") 'hippie-expand)
    (setq hippie-expand-try-functions-list
	  '(try-expand-dabbrev
	  try-expand-dabbrev-all-buffers
	  try-expand-dabbrev-from-kill
	  try-complete-file-name-partially
	  try-complete-file-name
	  try-expand-all-abbrevs
	  try-expand-list
	  try-expand-line
	  try-complete-lisp-symbol-partially
	  try-complete-lisp-symbol))
#+end_src

#+RESULTS:
| try-expand-dabbrev | try-expand-dabbrev-all-buffers | try-expand-dabbrev-from-kill | try-complete-file-name-partially | try-complete-file-name | try-expand-all-abbrevs | try-expand-list | try-expand-line | try-complete-lisp-symbol-partially | try-complete-lisp-symbol |

** Writing
*** Spell-check
I use flyspell together with a nice [[https://emacs.stackexchange.com/questions/14909/how-to-use-flyspell-to-efficiently-correct-previous-word][function]] by Mark Karpov that
opens the suggestion in a pop-up menu.
#+begin_src emacs-lisp
  (use-package flyspell
    :config
    ;; (setq ispell-program-name "/opt/local/bin/aspell")
    (setq ispell-program-name "/usr/bin/aspell")
    (flyspell-mode 1)
    (defun mk-flyspell-correct-previous (&optional words)
      "Correct word before point, reach distant words.

      WORDS words at maximum are traversed backward until misspelled
      word is found.  If it's not found, give up.  If argument WORDS is
      not specified, traverse 12 words by default.

      Return T if misspelled word is found and NIL otherwise.  Never
      move point."
      (interactive "P")
      (let* ((delta (- (point-max) (point)))
	     (counter (string-to-number (or words "12")))
	     (result
	      (catch 'result
		(while (>= counter 0)
		  (when (cl-some #'flyspell-overlay-p
				 (overlays-at (point)))
		    (flyspell-correct-word-before-point)
		    (throw 'result t))
		  (backward-word 1)
		  (setq counter (1- counter))
		  nil))))
	(goto-char (- (point-max) delta))
	result))
    (bind-key* "M-c" 'mk-flyspell-correct-previous))

  (use-package powerthesaurus)
#+end_src

#+RESULTS:

*** Grammar-check
#+begin_src emacs-lisp
  (use-package langtool
    :config
    (setq langtool-java-bin "/usr/bin/java")
    (setq langtool-language-tool-jar "/Applications/LanguageTool/languagetool-commandline.jar"))
#+end_src

#+RESULTS:
: t

I wrote a package called [[https://github.com/cjl8zf/langtool-ignore-fonts][langtool-ignore-fonts]] that extends the
langtool package to ignore specified fonts. I use this to ignore the
LaTeX markup when checking syntax. 
#+begin_src emacs-lisp
  ;; (use-package langtool-ignore-fonts
  ;;   :load-path "~/.emacs.d/elpa/"
  ;;   :config 
  ;;   (add-hook 'markdown-mode-hook (lambda () 
  ;; 				  (setq-local langtool-ignore-fonts '(markdown-code-face)))))
#+end_src

#+RESULTS:

** Project Management
#+begin_src emacs-lisp
  (use-package magit
    :init
    (add-hook 'after-save-hook 'magit-after-save-refresh-status t))

  (use-package diff-hl
    :config
    (add-hook 'magit-pre-refresh-hook 'diff-hl-magit-pre-refresh)
    (add-hook 'magit-post-refresh-hook 'diff-hl-magit-post-refresh))
#+end_src

#+RESULTS:
: t

#+begin_src emacs-lisp
  (use-package skeletor
    :config
    (setq skeletor-user-directory "/Users/chris/project-skeletons")
    (skeletor-define-template "latex-skeleton"
      :title "latex-skeleton"
      :no-license? t)
    (skeletor-define-template "beamer-skeleton"
      :title "beamer-skeleton"
      :no-license? t)
    (skeletor-define-template "classwork-skeleton"
      :title "classwork-skeleton"
      :no-license? t)
    (skeletor-define-template "coverletter-skeleton"
      :title "coverletter-skeleton"
      :no-license? t)
    (skeletor-define-template "resume-skeleton"
      :title "resume-skeleton"
      :no-license? t)
    (skeletor-define-template "c++-skeleton"
      :title "c++-skeleton"
      :no-license? t))
#+end_src

#+RESULTS:
: t

** Remote Work
I have a laptop I use as a server for running
computations. There is a cron job that writes the IP address of the laptop to
a file in Dropbox. These functions allow easy access to that computer.
#+begin_src emacs-lisp
  (use-package ssh)

  (defun connect-lenovo ()
    "Open dired into lenovo"
    (interactive)
    (dired (concat "/ssh:chris@" (lenovo-ip) ":/home/")))

  (defun ssh-lenovo ()
    "SSH into lenovo."
    (interactive)
    (ssh (concat "chris@" (lenovo-ip))))

  (defun lenovo-ip ()
    "Return ip for lenovo."
    (substring (get-string-from-file "~/DropBox/lenovo_ip.txt") 0 -1))

  (defun get-string-from-file (filePath)
    "Return filePath's file content."
    (with-temp-buffer
      (insert-file-contents filePath)
      (buffer-string)))
#+end_src

#+RESULTS:
: get-string-from-file

** Programming
*** Syntax
#+BEGIN_SRC emacs-lisp
  (use-package flycheck
    :init
    (global-flycheck-mode)
    (flycheck-define-checker proselint
      "A linter for prose."
      :command ("proselint" source-inplace)
      :error-patterns
      ((warning line-start (file-name) ":" line ":" column ": "
		(id (one-or-more (not (any " "))))
		(message) line-end))
      :modes (text-mode markdown-mode latex-mode))

    (add-to-list 'flycheck-checkers 'proselint))
#+END_SRC

#+RESULTS:

#+BEGIN_SRC emacs-lisp
(use-package aggressive-indent
  :config
  (global-aggressive-indent-mode 1))
#+END_SRC

#+RESULTS:
: t

*** Ctags
#+BEGIN_SRC emacs-lisp
  (use-package ctags-update)
#+END_SRC

#+RESULTS:

*** Company
#+BEGIN_SRC emacs-lisp
  (use-package company
    :custom
    (setq company-idle-delay 0)
    (add-to-list 'company-backends 'company-elisp)
    (global-company-mode))

  (use-package company-box
    :hook (company-mode . company-box-mode))
#+END_SRC

*** Language Server Protocol

#+BEGIN_SRC emacs-lisp
  (use-package lsp-mode
    :after company
    :custom
    (lsp-inlay-hint-enable nil)
    (lsp-completion-provider t)
    :config
    (add-to-list 'warning-suppress-log-types '(lsp-mode))
    (add-to-list 'warning-suppress-types '(lsp-mode)))

  (use-package lsp-ui)
#+END_SRC

#+RESULTS:

** Telemetry
#+begin_src emacs-lisp
  (use-package interaction-log)

  (use-package keyfreq
    :init
    (keyfreq-mode 1)
    (keyfreq-autosave-mode 1))
#+end_src

#+RESULTS:

*** Keybindings
#+begin_src emacs-lisp
  (general-define-key
   :keymaps 'mu4e-headers-mode-map
   "j" 'evil-next-line
   "k" 'evil-previous-line)

  (general-define-key
   :keymaps 'mu4e-view-mode-map
   "g" 'ace-link-mu4e)
#+end_src

#+RESULTS:

** Theme
Here we change the basic look and feel. 
#+begin_src emacs-lisp
  (setq inhibit-splash-screen t
	initial-scratch-message nil)

  (add-to-list 'initial-frame-alist '(fullscreen . maximized))
  (add-to-list 'default-frame-alist '(fullscreen . fullheight))

  (setq mouse-wheel-scroll-amount '(2 ((shift) . 2)))
  (setq mouse-wheel-progressive-speed nil)
  (setq mouse-wheel-follow-mouse 't)
  (setq scroll-step 1)

  (global-set-key [mouse-3]
    `(menu-item ,(purecopy "Menu Bar") ignore
      :filter (lambda (_)
	      (if (zerop (or (frame-parameter nil 'menu-bar-lines) 0))
		  (mouse-menu-bar-map)
		(mouse-menu-major-mode-map)))))

  (set-face-attribute 'default nil :height 140)

  (scroll-bar-mode -1)
  (tool-bar-mode -1)

  (use-package solarized-theme
	     :init (progn (load-theme 'solarized-dark t)))

  (use-package modern-fringes)

  ;; ensure that files openned from operating system open a buffer in
  ;; currently running emacs, and not in a new window
  (setq ns-pop-up-frames nil)

  ;; make shell commands work on OS X
  (use-package exec-path-from-shell
	     :config (when (memq window-system '(mac ns))
		       (exec-path-from-shell-initialize)))

  (defalias 'yes-or-no-p 'y-or-n-p)

  (setq confirm-kill-emacs 'yes-or-no-p)

  ;; update buffer when file changes on disk
  (global-auto-revert-mode t)

  (use-package smart-mode-line
   :ensure t
   :config
   (setq sml/theme 'dark)
   (setq rm-blacklist '(" Undo-Tree"
		      " ivy"
		      " counsel"
		      " yas"
		      " SP"
		      " WK"
		      " ElDoc"
		      " Fly"
		      " EvilOrg"
		      " s-/"))
   (add-hook 'after-init-hook 'sml/setup))
#+end_src

#+RESULTS:
: t
